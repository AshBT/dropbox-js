# The result of stat-ing a file or directory in a user's Dropbox.
class DropboxStat
  # Creates a Stat instance from a raw "metadata" response.
  #
  # @private
  # This constructor is used by Dropbox.Stat.parse, and should not be called
  # directly.
  #
  # @param {Object} metadata the result of parsing JSON API responses that are
  #     called "metadata" in the API documentation
  @parse: (metadata) ->
    if metadata and typeof metadata is 'object'
      new DropboxStat metadata
    else
      metadata
      
  # @return {String} the path of this file or folder, relative to the user's
  #     Dropbox or to the application's folder
  path: undefined

  # @return {Boolean} if true, the file or folder's path is relative to the
  #     application's folder; otherwise, the path is relative to the user's
  #     Dropbox
  inAppFolder: undefined

  # @return {Boolean} if true, this Stat instance describes a folder
  isFolder: undefined

  # @return {Boolean} if true, this Stat instance describes a file
  isFile: undefined

  # @return {Boolean} if true, the file or folder described by this Stat
  #     instance was from the user's Dropbox, and was obtained by an API call
  #     that returns deleted items
  isRemoved: undefined

  # @return {String} name of an icon in Dropbox's icon library that most
  #     accurately represents this file or folder
  #
  # See the Dropbox API documentation to obtain the Dropbox icon library.
  # https://www.dropbox.com/developers/reference/api#metadata
  typeIcon: undefined

  # @return {String} an identifier for the contents of the described file or
  #     directories; this can used to be restored a file's contents to a
  #     previous version, or to save bandwidth by not retrieving the same
  #     folder contents twice
  versionTag: undefined

  # @return {String} a guess of the MIME type representing the file or folder's
  #     contents
  mimeType: undefined

  # @return {Number} the size of the file, in bytes; undefined for folders
  size: undefined

  # @return {String} the size of the file, in a human-readable format, such as
  #     "225.4KB"; the format of this string is influenced by the API client's
  #     locale
  humanSize: undefined

  # @return {Boolean} if false, the URL generated by thumbnailUrl does not
  #     point to a valid image, and should not be used
  hasThumbnail: undefined

  # @return {Date} the file or folder's last modification time
  modifiedAt: undefined

  # @return {?Date} the file or folder's last modification time, as reported by
  #     the Dropbox client that uploaded the file; this time should not be
  #     trusted, but can be used for UI (display, sorting); null if the server
  #     does not report any time
  clientModifiedAt: undefined

  # Creates a Stat instance from a raw "metadata" response.
  #
  # @private
  # This constructor is used by Dropbox.Stat.parse, and should not be called
  # directly.
  #
  # @param {Object} metadata the result of parsing JSON API responses that are
  #     called "metadata" in the API documentation
  constructor: (metadata) ->
    @path = metadata.path
    @isFolder = metadata.is_dir || false
    @isFile = !@isFolder
    @isRemoved = metadata.is_deleted || false
    @typeIcon = metadata.icon
    if metadata.modified?.length
      @modifiedAt = new Date Date.parse(metadata.modified)
    else
      @modifiedAt = null
    if metadata.client_mtime?.length
      @clientModifiedAt = new Date Date.parse(metadata.client_mtime)
    else
      @clientModifiedAt = null

    switch metadata.root
      when 'dropbox'
        @inAppFolder = false
      when 'app_folder'
        @inAppFolder = true
      else
        # New "root" value that we're not aware of.
        @inAppFolder = null

    @size = metadata.bytes or 0
    @humanSize = metadata.size or ''
    @hasThumbnail = metadata.thumb_exists or false

    if @isFolder
      @versionTag = metadata.hash
      @mimeType = metadata.mime_type || 'inode/directory'
    else
      @versionTag = metadata.rev
      @mimeType = metadata.mime_type || 'application/octet-stream'
